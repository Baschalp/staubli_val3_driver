<?xml version="1.0" encoding="utf-8"?>
<Programs xmlns:xsi="http://www.w3.org/2001/XMLSchema-instance" xmlns="http://www.staubli.com/robotics/VAL3/Program/2">
  <Program name="decTrajPt" access="public">
    <Locals>
      <Local name="l_nRetVal" type="num" xsi:type="array" size="1" />
      <Local name="l_nIndex" type="num" xsi:type="array" size="1" />
    </Locals>
    <Code><![CDATA[begin
  // decode message ID 11 body (data)
  
  // sequence
  l_nRetVal = fromBinary(rosTrajPtMsg.body.nData, 4, "-4l", rosTrajPtMsg.jointTrajPt.nSeq)
  if (l_nRetVal != 1)
    put("data corrupted: ")
    putln(l_nRetVal)
    bValidMotion = false
    return
  endIf
  
  // joints[10]
  for l_nIndex = 0 to 9 step 1
    l_nRetVal = fromBinary(rosTrajPtMsg.body.nData[(l_nIndex*4)+4], 4, "4.0l", rosTrajPtMsg.jointTrajPt.nJoints[l_nIndex])
    if (l_nRetVal != 1)
      put("data corrupted: ")
      putln(l_nRetVal)
      bValidMotion = false
      return
    endIf
  endFor
  
  // velocity
  l_nRetVal = fromBinary(rosTrajPtMsg.body.nData[44], 4, "4.0l", rosTrajPtMsg.jointTrajPt.nVelocity)
  if (l_nRetVal != 1)
    put("data corrupted: ")
    putln(l_nRetVal)
    bValidMotion = false
    return
  endIf  
  
  // duration
  l_nRetVal = fromBinary(rosTrajPtMsg.body.nData[48], 4, "4.0l", rosTrajPtMsg.jointTrajPt.nDuration)
  if (l_nRetVal != 1)
    put("data corrupted: ")
    putln(l_nRetVal)
    bValidMotion = false
    return
  endIf
  
  rosTrajPtAck.header.nReplyCode = 1
  bValidMotion = true
end]]></Code>
  </Program>
</Programs>